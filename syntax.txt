# switch-extension syntax
# refer xfrp syntax(https://github.com/psg-titech/xfrp/blob/master/syntax.txt)

# Legends
#   foo      : nonterminal symbol
#   'foo'    : terminal symbol
#   [ ... ]  : optional
#   { ... }* : repetition (0 or more times)
#   { ... }+ : repetition (1 or more times)
#
# Grammar Definitions
#   foo ::= ...  : definition in EBNF
#   foo = ...    : definition in regular expression


### whole module ###
program ::= xfrpModule | xfrpSModule

xfrpModule ::=
  'module' uidentifier
  ['in'  nodeDecl {',' nodeDecl}*]
  ['out' nodeDecl {',' nodeDecl}*]
  ['use' uidentifier {',' uidentifier}*]
  { definitionM }+

xfrpSModule ::=
   'switchmodule' uidentifier
  ['in'  nodeDecl {',' nodeDecl}*]
  ['out' nodeDecl {',' nodeDecl}*]
  ['use' uidentifier {',' uidentifier}*]
  'init' identifier literal
  { definitionSM }+

nodeDecl ::= identifier ['(' literal ')'] : typeSpec

### toplevel definitions ###
definitionM ::=
  | constDefinition
  | typeDefinition
  | funDefinition
  | nodeDefinition

definitionSM ::=
  | constDefinition
  | typeDefinition
  | funDefinition
  | stateDefinition

constDefinition ::= 'const' idAndTypeOpt '=' expression

typeDefinition ::= 'type' identifier '='
                   variantDefinition {'|' variantDefinition}*
variantDefinition ::= identifier ['of' typeSpec]

funDefinition ::=
  'fun' identifier '(' [idAndTypeOpt {',' idAndTypeOpt}* ] ')' [':' typeSpec]
  '=' expression

nodeDefinition ::= 'node' ['init' '[' expression ']'] idAndTypeOpt '=' expression

stateDefinition ::=
  'state' identifier ['(' idAndType {',' idAndType}*  ')'] '{'
  {nodeDefinition}+
  'switch:' expression
  '}'

### expression ###
expression ::=
    uniOp expression
  | expression binOp expression
  | '(' expression {',' expression}* ')'                 # tuple or paren
  | uidentifier [expression]                             # variant
  | literal                                              # constant
  | 'Retain'                                             # Retain
  | identifier                                           # identifier
  | identifier '@' annotation                            # node with annotation
  | identifier '(' [expression {',' expression}*] ')'    # funcall
  | 'if' expression 'then' expression 'else' expression  # if-expr
  | 'let' {binder}+ 'in' expression                      # let-expr
  | 'case' expression 'of' {branch}+                     # pattern match
binder ::= idAndTypeOpt '=' expression
branch :: = pattern '->' expression ';'

uniOp ::= '+' | '-' | '!' | '~'

binOp ::=
    '*' | '/' | '+' | '-'
  | '*.' | '/.' | '+.' | '-.'
  | '%'| '<<' | '>>'
  | '<' | '<=' | '>=' | '>'
  | '<.' | '<=.' | '>=.' | '>.'
  | '==' | '!='
  | '&'
  | '^'
  | '|'
  | '&&'
  | '||'

annotation ::= 'last'

pattern ::=
    '_'
  | identifier
  | literal
  | '(' pattern {',' pattern}* ')' # tuple
  | uidentifier pattern            # ADT

### primitives ###
identifier = [a-z][A-Za-z0-9_]*
uidentifier = [A-Z][A-Za-z0-9_]*

idAndType ::= identifier ':' typeSpec
idAndTypeOpt ::= identifier [':' typeSpec]

literal ::=
    'True'
  | 'False'
  | number
  | variantLiteral
  | '(' [literal {',' literal}*] ')'
# | charLiteral

variantLiteral ::= uidentifier [literal]

number ::=
     decimalNumber
   | floatNumber
#  | octalNumber
#  | hexadecimalNumber

typeSpec ::=
    'Bool' | 'Int' | 'Float' | 'Unit'
  | identifier
  | '(' typeSpec {',' typeSpec}* ')'
# | 'Char'
