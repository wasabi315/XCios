fun hoge(x,y) = x == y

switchmodule Main {
  in   clock(0)   : Int,
       di         : Float
  out  ctime(0)   : Int,
       otime(0)   : Int,
       fan(False) : Bool
  init FanOff

  state FanOff {
    out ctime = ctime@last

    out otime = 0

    out fan = False

    switch :
      if di >=. 75.5 then FanOn(clock)
      else Retain
  }

  state FanOn(begin : Int) {
    out ctime =
      let dt = clock - clock@last in
      ctime@last + dt

    out otime = clock - begin

    out fan = True

    switch :
      if di <=. 74.5 then FanOff
      else if otime > 7200 then FanSleep
      else Retain
  }

  state FanSleep {
    out ctime = ctime@last

    out otime = 0

    out fan = False

    node timer(1800) =
      let dt = clock - clock@last in
      timer@last - dt

    switch :
      if timer <= 0 then FanOff else Retain
  }
}
